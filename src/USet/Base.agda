{-# OPTIONS --safe --without-K #-}

open import Frame.IFrame
import Frame.CFrame as CF

module USet.Base
  {W    : Set}
  {_âŠ†_  : (w w' : W) â†’ Set}
  (IF   : IFrame W _âŠ†_)
  (let open CF IF)
  (ğ’¦   : KPsh)
  (let open KPsh ğ’¦)
  (_âˆˆ_ : (v : W) {w : W} â†’ K w â†’ Set)
  (let open Core ğ’¦ _âˆˆ_)
  (CF : CFrame)
  where

open import Function using (id ; _âˆ˜_)
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; subst; cong; congâ‚‚)
  renaming (refl to â‰¡-refl; sym to â‰¡-sym; trans to â‰¡-trans)
open import Relation.Binary.PropositionalEquality.Properties
  using () renaming (isEquivalence to â‰¡-equiv)

open import Data.Product
  using (Î£; âˆƒ; _Ã—_; _,_; -,_ ; projâ‚ ; projâ‚‚ ; uncurry)

private
  variable
    w w' w'' u u' v v' : W

open IFrame IF
open CFrame CF

-- Upper set
record USet : Setâ‚ where
  constructor uset
  field
    Fam : W â†’ Set
    wk  : w âŠ† w' â†’ Fam w â†’ Fam w'

open import Data.Sum

_Ã—'_ : USet â†’ USet â†’ USet
(uset X wkX) Ã—' (uset Y wkY) = uset (Î» w â†’ X w Ã— Y w) wkÃ—
  where
  wkÃ— : w âŠ† w' â†’ X w Ã— Y w â†’ X w' Ã— Y w'
  wkÃ— i (x , y) = (wkX i x) , (wkY i y)

_âŠ'_ : USet â†’ USet â†’ USet
(uset X wkX) âŠ' (uset Y wkY) = uset (Î» w â†’ X w âŠ Y w) wk+
  where
  wk+ : w âŠ† w' â†’ X w âŠ Y w â†’ X w' âŠ Y w'
  wk+ i (injâ‚ x) = injâ‚ (wkX i x)
  wk+ i (injâ‚‚ y) = injâ‚‚ (wkY i y)

_â†’'_ : USet â†’ USet â†’ USet
(uset X wkX) â†’' (uset Y wkY) = uset (Î» w â†’ âˆ€ {w'} â†’ w âŠ† w' â†’ X w' â†’ Y w') wkâ†’
  where
  wkâ†’ : {w w' : W} â†’ w âŠ† w'
    â†’ ({w1 : W} â†’ w âŠ† w1 â†’ X w1 â†’ Y w1)
    â†’ {w2 : W} â†’ w' âŠ† w2 â†’ X w2 â†’ Y w2
  wkâ†’ i f = Î» i' x â†’ f (âŠ†-trans i i') x

open USet renaming (Fam to _â‚€_) public

Cover' : USet â†’ USet
Cover' A = uset CoverFam wkCov
  where
  CoverFam : W â†’ Set
  CoverFam = Î» w â†’ Î£ (K w) Î» k â†’ ForAllW k Î» v â†’ A â‚€ v

  wkCov : w âŠ† w' â†’ CoverFam w â†’ CoverFam w'
  wkCov i (k , f) = wkK i k , Î» p â†’ wk A (factorâŠ† i k p) (f (factorâˆˆ i k p))

record _â†’Ì‡_ (X Y : USet) : Set where
  constructor fun
  field
    apply : âˆ€ {w} â†’ X â‚€ w â†’ Y â‚€ w

open _â†’Ì‡_ public

id' : {A : USet} â†’ A â†’Ì‡ A
id' .apply = id

_âˆ˜'_ : {A B C : USet} â†’ B â†’Ì‡ C â†’ A â†’Ì‡ B â†’ A â†’Ì‡ C
(f âˆ˜' g) .apply = f .apply âˆ˜ g .apply

injâ‚' : {A B : USet} â†’ A â†’Ì‡ (A âŠ' B)
injâ‚' .apply = injâ‚

injâ‚‚' : {A B : USet} â†’ B â†’Ì‡ (A âŠ' B)
injâ‚‚' .apply = injâ‚‚

[_,_]' : {A B C : USet} â†’  A â†’Ì‡ C â†’ B â†’Ì‡ C â†’ (A âŠ' B) â†’Ì‡  C
[ f , g ]' .apply = [ f .apply , g .apply ]

mapCover' : {A B : USet} â†’ (f : A â†’Ì‡ B) â†’ Cover' A â†’Ì‡ Cover' B
mapCover' f .apply (k , g) = k , f .apply âˆ˜ g

module _ {A B : USet} (run : {w : W} (k : K w) (f : ForAllW k (A â‚€_)) â†’ B â‚€ w) where

  runCover : Cover' A â†’Ì‡ B
  runCover .apply = uncurry run

module Return (PCF : Pointed CF) where
  open Pointed PCF

  return' : {A : USet} â†’ A â†’Ì‡ Cover' A
  return' {A} .apply {w} x = pointK[ w ] , Î» v â†’ subst (A â‚€_) (pointâˆˆ v) x

module Join (JCF : Joinable CF) where
  open Joinable JCF

  join' : {A : USet} â†’ Cover' (Cover' A) â†’Ì‡ Cover' A
  join' {A} .apply {w} (k , f) = joinK k (projâ‚ âˆ˜ f) , Î» e â†’
    let _ , eâ‚ , eâ‚‚ = joinâˆˆ k (projâ‚ âˆ˜ f) e
    in  f eâ‚ .projâ‚‚ eâ‚‚

module Extract (CPCF : CoPointed CF) where
  open CoPointed CPCF

  extract' : {A : USet} â†’ Cover' A â†’Ì‡ A
  extract' {A} .apply {w} (k , f) = f (copointâˆˆ k)

module Cojoin (CJCF : CoJoinable CF) where
  open CoJoinable CJCF

  cojoin' : {A : USet} â†’ Cover' A â†’Ì‡ Cover' (Cover' A)
  cojoin' {A} .apply {w} (k , f) = k , Î» p â†’ cojoinK k p , Î» p' â†’ f (cojoinâˆˆ k p p')
