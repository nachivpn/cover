{-# OPTIONS --safe #-}

-- Dual Context K calculus
module Instances.DK where

open import Data.Product
  using (Î£; âˆƒ; âˆƒâ‚‚; _Ã—_; _,_; -,_ ; projâ‚ ; projâ‚‚ ; curry ; uncurry)

open import PUtil

open import Function

infix  3  _â¨¾_âŠ¢_
infix  3  _â¨¾_âŠ¢Ne_
infix  3  _â¨¾_âŠ¢Nf_

data Ty : Set where
  ğ•“  : Ty
  _â‡’_ : Ty â†’ Ty â†’ Ty
  â—»_ : Ty â†’ Ty

private
  variable
    a b c d : Ty

open import Context Ty

data _â¨¾_âŠ¢_ (Î” Î“ : Ctx) : Ty â†’ Set where
  var   : (x : Var Î“ a) â†’ Î” â¨¾ Î“ âŠ¢ a
  lam   : Î” â¨¾ (Î“ `, a) âŠ¢ b â†’ Î” â¨¾ Î“ âŠ¢ (a â‡’ b)
  app   : Î” â¨¾ Î“ âŠ¢ (a â‡’ b) â†’ Î” â¨¾ Î“ âŠ¢ a â†’ Î” â¨¾ Î“ âŠ¢ b
  box   : (t : [] â¨¾ Î” âŠ¢ a) â†’  Î” â¨¾ Î“ âŠ¢ (â—» a)
  letin : (t : Î” â¨¾ Î“ âŠ¢ (â—» a)) â†’ (u : (Î” `, a) â¨¾ Î“ âŠ¢ b) â†’  Î” â¨¾ Î“ âŠ¢ b

mutual
  data _â¨¾_âŠ¢Ne_ (Î” Î“ : Ctx) : Ty â†’ Set where
    var : Var Î“ a â†’ Î” â¨¾ Î“ âŠ¢Ne a
    app : Î” â¨¾  Î“ âŠ¢Ne (a â‡’ b) â†’ Î” â¨¾ Î“ âŠ¢Nf a â†’ Î” â¨¾ Î“ âŠ¢Ne b

  data _â¨¾_âŠ¢Nf_ (Î” Î“ : Ctx) : Ty â†’ Set where
    emb   : Î” â¨¾ Î“ âŠ¢Ne ğ•“ â†’ Î” â¨¾ Î“ âŠ¢Nf ğ•“
    lam   : Î” â¨¾ (Î“ `, a) âŠ¢Nf b â†’ Î” â¨¾ Î“ âŠ¢Nf (a â‡’ b)
    box   : [] â¨¾ Î” âŠ¢Nf a â†’ Î” â¨¾ Î“ âŠ¢Nf â—» a
    letin : Î” â¨¾ Î“ âŠ¢Ne â—» a â†’ Î” `, a â¨¾ Î“ âŠ¢Nf â—» b â†’ Î” â¨¾ Î“ âŠ¢Nf â—» b

wkNe : Î” âŠ† Î”' â†’ Î“ âŠ† Î“' â†’ Î” â¨¾ Î“ âŠ¢Ne a â†’ Î”' â¨¾ Î“' âŠ¢Ne a
wkNf : Î” âŠ† Î”' â†’ Î“ âŠ† Î“' â†’ Î” â¨¾ Î“ âŠ¢Nf a â†’ Î”' â¨¾ Î“' âŠ¢Nf a

wkNe _  i  (var x)   = var (wkVar i x)
wkNe i1 i2 (app n m) = app (wkNe i1 i2 n) (wkNf i1 i2 m)

wkNf i1 i2 (emb x)     = emb (wkNe i1 i2 x )
wkNf i1 i2 (lam x)     = lam (wkNf i1 (keep i2) x)
wkNf i1 i2 (box n)     = box (wkNf base i1 n)
wkNf i1 i2 (letin x n) = letin (wkNe i1 i2 x) (wkNf (keep i1) i2 n)

data Box (A : Ctx â†’ Ctx â†’ Set) (Î” Î“ : Ctx) : Set where
  box    : A [] Î” â†’ Box A Î” Î“
  letbox : Î” â¨¾ Î“ âŠ¢Ne (â—» a) â†’ Box A (Î” `, a) Î“ â†’ Box A Î” Î“

data K : Ctx â†’ Ctx â†’ Set where
  single : (Î” : Ctx) (Î“ : Ctx) â†’ K Î” Î“
  cons   : Î” â¨¾ Î“ âŠ¢Ne (â—» a) â†’ K (Î” `, a) Î“ â†’ K Î” Î“

data _â¨¾_âˆˆ_ : Ctx â†’ Ctx â†’ K Î” Î“ â†’ Set where
  here  : [] â¨¾ Î âˆˆ single Î Î˜
  there : {n : Î” â¨¾ Î“ âŠ¢Ne (â—» a)} {k : K (Î” `, a) Î“}
        â†’ Î â¨¾ Î˜ âˆˆ k â†’ Î â¨¾ Î˜ âˆˆ cons n k

thereâ»Â¹ : {n : Î” â¨¾ Î“ âŠ¢Ne (â—» a)} {k : K (Î” `, a) Î“}
  â†’ Î â¨¾ Î˜ âˆˆ cons n k â†’ Î â¨¾ Î˜ âˆˆ k
thereâ»Â¹ (there x) = x

Ctxâ‚‚ : Set
Ctxâ‚‚ = Ctx Ã— Ctx

private
  variable
    Î§ Î§' Î§'' Î§''' : Ctxâ‚‚

_âŠ†â‚‚_ : Ctx Ã— Ctx â†’ Ctx Ã— Ctx â†’ Set
(Î” , Î“) âŠ†â‚‚ (Î”' , Î“') = Î” âŠ† Î”' Ã— Î“ âŠ† Î“'

âŠ†â‚‚-trans : Î§ âŠ†â‚‚ Î§' â†’ Î§' âŠ†â‚‚ Î§'' â†’ Î§ âŠ†â‚‚ Î§''
âŠ†â‚‚-trans (i1 , i2) (i1' , i2') = âŠ†-trans i1 i1' , âŠ†-trans i2 i2'

âŠ†â‚‚-refl : Î§ âŠ†â‚‚ Î§
âŠ†â‚‚-refl = âŠ†-refl , âŠ†-refl


open import Frame.IFrame

ğ•â‚‚ : Preorder Ctxâ‚‚ _âŠ†â‚‚_
ğ•â‚‚ = record
      { âŠ†-trans            = âŠ†â‚‚-trans
      ; âŠ†-refl             = âŠ†â‚‚-refl
      }

wkK : Î” âŠ† Î”' â†’ Î“ âŠ† Î“' â†’ K Î” Î“ â†’ K Î”' Î“'
wkK i1 i2 (single _ _) = single _ _
wkK i1 i2 (cons x k)   = cons (wkNe i1 i2 x) (wkK (keep i1) i2 k)

Kâ‚‚ = uncurry K

wkKâ‚‚ : Î§ âŠ†â‚‚ Î§' â†’ Kâ‚‚ Î§ â†’ Kâ‚‚ Î§'
wkKâ‚‚ = uncurry wkK

_âˆˆ_ : Ctxâ‚‚ â†’ âˆ€ {Î§} â†’ Kâ‚‚ Î§ â†’ Set
Î§ âˆˆ k = uncurry (_â¨¾_âˆˆ k) Î§

open import Frame.NFrame ğ•â‚‚ Kâ‚‚ _âˆˆ_

wkK-refines : (i1 : Î” âŠ† Î”') (i2 : Î“ âŠ† Î“') (k : K Î” Î“)
  â†’ k â‰¼ wkK i1 i2 k
wkK-refines i1 i2 (single _ _) here      = _ , here , base , i1
wkK-refines i1 i2 (cons x k)   (there p) =
  let (_ , p' , i1' , i2') = wkK-refines (keep i1) i2 k p
  in _ , there p' , i1' , i2'

wkKâ‚‚-refinesâ‚‚ : (i : Î§ âŠ†â‚‚ Î§') (k : Kâ‚‚ Î§) â†’ k â‰¼ wkKâ‚‚ i k
wkKâ‚‚-refinesâ‚‚ = uncurry wkK-refines

NF : Refinement
NF = record { wkN = wkKâ‚‚ ; wkN-refines = wkKâ‚‚-refinesâ‚‚ }

_âŠ—_ : K Î” Î“ â†’ K Î” Î“ â†’ K Î” Î“
single Î” Î“ âŠ— k' = k'
cons x k   âŠ— k' = cons x (k âŠ— wkK freshWk âŠ†-refl k')

âˆˆ-fwd-reachable : (k : K Î” Î“) â†’ Î â¨¾ Î˜ âˆˆ k â†’ Î âŠ† Î“
âˆˆ-fwd-reachable (single Î” Î“) here      = âŠ†-init[ Î“ ]
âˆˆ-fwd-reachable (cons x k)   (there p) = âˆˆ-fwd-reachable k p

âˆˆ-bwd-reachable : (k : K Î” Î“) â†’ Î â¨¾ Î˜ âˆˆ k â†’ Î” âŠ† Î˜
âˆˆ-bwd-reachable (single Î” Î“) here = âŠ†-refl[ Î” ]
âˆˆ-bwd-reachable (cons x k)   (there p) = freshWk âˆ™ âˆˆ-bwd-reachable k p

âˆˆ-bwd-reachableâ‚‚ : (k : K Î” Î“) â†’ Î â¨¾ Î˜ âˆˆ k â†’ ([] , Î”) âŠ†â‚‚ (Î , Î˜)
âˆˆ-bwd-reachableâ‚‚ k p = âŠ†-init[ _ ] , âˆˆ-bwd-reachable k p

âŠ—-bwd-reachable : (k1 k2 : K Î” Î“) â†’ ForAllW (k1 âŠ— k2)
     (Î» Î§' â†’ âˆƒâ‚‚ (Î» Î§1 Î§2 â†’ Î§1 âˆˆ k1 Ã— Î§1 âŠ†â‚‚ Î§' Ã— Î§2 âˆˆ k2 Ã— Î§2 âŠ†â‚‚ Î§'))
âŠ—-bwd-reachable (single Î” Î“) k      {Î , Î˜}       p
  = ([] , Î”) , (Î , Î˜)
  , here , âˆˆ-bwd-reachableâ‚‚ k p
  , p    , âŠ†â‚‚-refl
âŠ—-bwd-reachable (cons x k1) k2       {Î , Î˜}     (there p)
  = let ((Î”1 , Î“1) , (Î”2 , Î“2) , p1 , i1 , p2 , i2) = âŠ—-bwd-reachable k1 (wkK freshWk âŠ†-refl k2) p
        ((Î”2' , Î“2') , p2' , i2') = wkK-refines freshWk âŠ†-refl k2 p2
    in _ , _
      , there p1 , i1
      , p2' , âŠ†â‚‚-trans i2' i2

WCNF : WeaklyClosedUnderInt
WCNF = record { _âŠ—_ = _âŠ—_ ; âŠ—-bwd-reachable = âŠ—-bwd-reachable }

unitK : âˆ€ Î§ â†’ Kâ‚‚ Î§
unitK Î§ = single _ _

UNF : NonEmpty
UNF = record { unitN[_] = unitK }

open import USet.Base ğ•â‚‚
open import USet.Cover ğ•â‚‚ Kâ‚‚ _âˆˆ_ NF renaming (ğ’' to Box')

box' : {A : USet} â†’ A â‚€ ([] , Î”) â†’ Box' A â‚€ (Î” , Î“)
box' x = (single _ _) , (Î» { here â†’ x })

Nf' : Ty â†’ USet
Nf' a = uset (uncurry (_â¨¾_âŠ¢Nf a)) (uncurry wkNf)

Ne' : Ty â†’ USet
Ne' a = uset (uncurry (_â¨¾_âŠ¢Ne a)) (uncurry wkNe)

emb' : Ne' ğ•“ â†’Ì‡ Nf' ğ•“
emb' .apply = emb

âŸ¦_âŸ§ : Ty â†’ USet
âŸ¦ ğ•“     âŸ§ = Nf' ğ•“
âŸ¦ a â‡’ b âŸ§ = âŸ¦ a âŸ§ â†’' âŸ¦ b âŸ§
âŸ¦ â—» a   âŸ§ = Box' (âŸ¦ a âŸ§)

âŸ¦_âŸ§c : Ctx â†’ USet
âŸ¦ [] âŸ§c     = âŠ¤'
âŸ¦ Î“ `, a âŸ§c = âŸ¦ Î“ âŸ§c Ã—' âŸ¦ a âŸ§

âŸ¦_âŸ§câ‚‚ : Ctxâ‚‚ â†’ USet
âŸ¦ Î” , Î“ âŸ§câ‚‚ = Box' âŸ¦ Î” âŸ§c Ã—' âŸ¦ Î“ âŸ§c

evalVar : Var Î“ a â†’  âŸ¦ Î“ âŸ§c â†’Ì‡ âŸ¦ a âŸ§
evalVar zero     = projâ‚‚'
evalVar (succ x) = evalVar x âˆ˜'  projâ‚'

letin' : {D G A B : USet}
  â†’ (Box' D Ã—' G) â†’Ì‡ Box' A
  â†’ (Box' (D Ã—' A) Ã—' G) â†’Ì‡ B
  â†’ (Box' D Ã—' G) â†’Ì‡ B
letin' {D} {G} {A} = Ã—'-distr.letin' WCNF {D = D} {A = A}

prBox' : {G A B : USet} â†’ G â†’Ì‡ Box' A â†’ G â†’Ì‡ Box' B â†’ G â†’Ì‡ Box' (A Ã—' B)
prBox' {G} {A} {B} = Ã—'-distr.prğ’' WCNF {G = G} {A = A} {B = B}

unitBox' : {G : USet} â†’ G â†’Ì‡ Box' âŠ¤'
unitBox' = âŠ¤'-distr.unitğ’' UNF

eval : Î” â¨¾ Î“ âŠ¢ a â†’ âŸ¦ Î” , Î“ âŸ§câ‚‚ â†’Ì‡ âŸ¦ a âŸ§
eval (var x)
  = evalVar x âˆ˜' projâ‚‚'
eval (lam {a = a} {b} t)
  = lam' {A = âŸ¦ a âŸ§} {B = âŸ¦ b âŸ§} (eval t âˆ˜' x'-right-assoc)
eval (app t u)
  = app' (eval t) (eval u)
eval {Î”} {Î“} (box {a = a} t)
  = mapğ’' {A = âŸ¦ Î” âŸ§c} {B = âŸ¦ a âŸ§} (eval t âˆ˜' âŸ¨ unitBox' {G = âŸ¦ Î” âŸ§c } , id' âŸ©') âˆ˜' projâ‚'
eval {Î”} (letin {a = a} t u)
  = letin' {D = âŸ¦ Î” âŸ§c} {A = âŸ¦ a âŸ§} (eval t) (eval u)

--
-- Residualisation
--

collect : Box' (Nf' a) â†’Ì‡ Nf' (â—» a)
collect {a} = runğ’' {Nf' a} collectAux
  where
  collectAux : (k : Kâ‚‚ Î§) (f : ForAllW k (Nf' a â‚€_)) â†’ Nf' (â—» a) â‚€ Î§
  collectAux (single _ _) f = box (f here)
  collectAux (cons n k)   f = letin n (collectAux k (f âˆ˜ there))

register : Ne' (â—» a) â†’Ì‡ Box' (Ne' a)
register {a} .apply {Î“} n = cons n (single _ _) , Î» { (there here) â†’ var zero }

reify   : âˆ€ a â†’ âŸ¦ a âŸ§ â†’Ì‡ Nf' a
reflect : âˆ€ a â†’ Ne' a â†’Ì‡ âŸ¦ a âŸ§

reify ğ•“       = id'
reify (a â‡’ b) = fun Î» f â†’ lam (reify b .apply (f (âŠ†-refl , freshWk) (reflect a .apply (var zero))))
reify (â—» a)   = collect âˆ˜' mapğ’' (reify a)

reflect ğ•“       = emb'
reflect (a â‡’ b) = fun Î» n i x â†’ reflect b .apply (app (uncurry wkNe i n) (reify a .apply x))
reflect (â—» a)   = mapğ’' (reflect a) âˆ˜' register

--
-- NbE
--

idEnv : âˆ€ Î§ â†’ âŸ¦ Î§ âŸ§câ‚‚ â‚€ Î§
idEnv (Î” , Î“) = idEnv1 Î” Î“ , idEnv2 Î” Î“
  where
  idEnv1 : âˆ€ Î” Î“ â†’ Box' âŸ¦ Î” âŸ§c â‚€ (Î” , Î“)
  idEnv1 []       Î“ = single [] Î“ , Î» x â†’ _
  idEnv1 (Î” `, a) Î“ = prBox' {G = Box' âŸ¦ Î” âŸ§c Ã—' Box' âŸ¦ a âŸ§} {A = âŸ¦ Î” âŸ§c} {B = âŸ¦ a âŸ§} projâ‚' projâ‚‚' .apply
    (wk (Box' âŸ¦ Î” âŸ§c) (freshWk , âŠ†-refl) (idEnv1 Î” Î“)
    , box' {A = âŸ¦ a âŸ§} (reflect a .apply (var zero)))

  idEnv2 : âˆ€ Î” Î“ â†’ âŸ¦ Î“ âŸ§c â‚€ (Î” , Î“)
  idEnv2 Î” []       = _
  idEnv2 Î” (Î“ `, a) = wk âŸ¦ Î“ âŸ§c (âŠ†-refl , freshWk) (idEnv2 Î” Î“) , reflect a .apply (var zero)

quot : (âŸ¦ Î” , Î“ âŸ§câ‚‚ â†’Ì‡ âŸ¦ a âŸ§) â†’ Î” â¨¾ Î“ âŠ¢Nf a
quot {Î”} {Î“} {a} f = reify a .apply (f .apply (idEnv (Î” , Î“)))

norm : Î” â¨¾ Î“ âŠ¢ a â†’ Î” â¨¾ Î“ âŠ¢Nf a
norm = quot âˆ˜ eval

module Equiv where

  ğ’' : USet â†’ USet
  ğ’' Aâ‚‚ = uset (uncurry (Box A)) (uncurry wkBox)
    where

    A : Ctx â†’ Ctx â†’ Set
    A = curry (Aâ‚‚ â‚€_)

    wkBox : Î” âŠ† Î”' â†’ Î“ âŠ† Î“' â†’ Box A Î” Î“ â†’ Box A Î”' Î“'
    wkBox i1 i2 (box x)      = box (curry (wk Aâ‚‚) base i1 x)
    wkBox i1 i2 (letbox x b) = letbox (wkNe i1 i2 x) (wkBox (keep i1) i2 b)

  to : {A : USet} â†’ ğ’' A â†’Ì‡ Box' A
  to {A} .apply (box x)      = single _ _ , Î» { here â†’ x }
  to {A} .apply (letbox x m) =
    let (k , f) = to {A} .apply m
      in cons x k , f âˆ˜ thereâ»Â¹

  fromAux : {A : USet} {Î§ : Ctxâ‚‚} â†’ (k : Kâ‚‚ Î§) (f : ForAllW k (A â‚€_)) â†’ ğ’' A â‚€ Î§
  fromAux {A} (single _ _) f = box (f here)
  fromAux {A} (cons x k)   f = letbox x (fromAux {A} k (f âˆ˜ there))

  from : {A : USet} â†’ Box' A â†’Ì‡ ğ’' A
  from {A} = runğ’' {A} (fromAux {A})
