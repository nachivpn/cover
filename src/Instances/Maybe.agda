{-# OPTIONS --safe #-}

module Instances.Maybe where

open import Data.Product
  using (Î£; âˆƒ; _Ã—_; _,_; -,_ ; projâ‚ ; projâ‚‚)

open import Relation.Binary.PropositionalEquality using (_â‰¡_)
  renaming (refl to â‰¡-refl ; sym to â‰¡-sym ; trans to â‰¡-trans
  ; cong to â‰¡-cong ; congâ‚‚ to â‰¡-congâ‚‚ ; subst to â‰¡-subst)

open import PUtil

open import Function
open import Data.Sum

data Ty : Set where
  ğ•“   : Ty
  _â‡’_ : Ty â†’ Ty â†’ Ty
  ğ•   : Ty â†’ Ty

private
  variable
    a b c d : Ty

open import Context Ty

--
-- Syntax
--

data Tm : Ctx â†’ Ty â†’ Set where
  var     : Var Î“ a â†’ Tm Î“ a
  lam     : Tm (Î“ `, a) b â†’ Tm Î“ (a â‡’ b)
  app     : Tm Î“ (a â‡’ b) â†’ Tm Î“ a â†’ Tm Î“ b
  nothing : Tm Î“ (ğ• a)
  just    : Tm Î“ a â†’ Tm Î“ (ğ• a)
  letin   : Tm Î“ (ğ• a) â†’ Tm (Î“ `, a) (ğ• b) â†’ Tm Î“ (ğ• b)

data Ne : Ctx â†’ Ty â†’ Set
data Nf : Ctx â†’ Ty â†’ Set

data Ne where
  var : Var Î“ a â†’ Ne Î“ a
  app : Ne Î“ (a â‡’ b) â†’ Nf Î“ a â†’ Ne Î“ b

data Nf where
  emb     : Ne Î“ ğ•“ â†’ Nf Î“ ğ•“
  lam     : Nf (Î“ `, a) b â†’ Nf Î“ (a â‡’ b)
  just    : Nf Î“ a â†’ Nf Î“ (ğ• a)
  nothing : Nf Î“ (ğ• a)
  letin   : Ne Î“ (ğ• a) â†’ Nf (Î“ `, a) b â†’ Nf Î“ b

wkNe : Î“ âŠ† Î“' â†’ Ne Î“ a â†’ Ne Î“' a
wkNf : Î“ âŠ† Î“' â†’ Nf Î“ a â†’ Nf Î“' a

wkNe i (var x)   = var (wkVar i x)
wkNe i (app n x) = app (wkNe i n) (wkNf i x)

wkNf i (emb x)     = emb (wkNe i x)
wkNf i (lam n)     = lam (wkNf (keep i) n)
wkNf i (just n)    = just (wkNf i n)
wkNf i nothing     = nothing
wkNf i (letin n m) = letin (wkNe i n) (wkNf (keep i) m)

--
-- Semantics
--

open import Frame.NFrame ğ•

-- the concrete residualising monad (for illustration only)
data Maybe (A : Ctx â†’ Set) : Ctx â†’ Set where
  return   : A Î“ â†’ Maybe A Î“
  nothing  : Maybe A Î“
  letin    : Ne Î“ (ğ• a) â†’ Maybe A (Î“ `, a) â†’ Maybe A Î“

-- Maybe reconstructed using K and âˆˆ
data K : Ctx â†’ Set where
  single : (Î“ : Ctx) â†’ K Î“
  nil    : K Î“
  cons   : Ne Î“ (ğ• a) â†’ K (Î“ `, a) â†’ K Î“

data _âˆˆ_ (Î” : Ctx) : K Î“ â†’ Set where
  here  : Î” âˆˆ single Î”
  there : {n : Ne Î“ (ğ• a)} {k : K (Î“ `, a)} â†’ Î” âˆˆ k â†’ Î” âˆˆ cons n k

wkK : Î“ âŠ† Î“' â†’ K Î“ â†’ K Î“'
wkK i (single _) = single _
wkK i nil        = nil
wkK i (cons n k) = cons (wkNe i n) (wkK (keep i) k)

open {-NF.-}Core K _âˆˆ_

wkK-resp-âŠ† : (i : Î“ âŠ† Î“') (k : K Î“) â†’ k âŠ†k wkK i k
wkK-resp-âŠ† i (single _)   here
  = _ , here , i
wkK-resp-âŠ† i nil          ()
wkK-resp-âŠ† i (cons n k)   (there p)
  = let (Î” , p' , i') = wkK-resp-âŠ† (keep i) k p in
     (Î” , there p' , i')

NF : NFrame
NF = record { wkK = wkK ; wkK-resp-âŠ† = wkK-resp-âŠ† }

open Reachable

RNF : Reachable NF
RNF .reachable (single Î“) here      = âŠ†-refl
RNF .reachable (cons n k) (there x) = freshWk âˆ™ RNF .reachable k x

SPNF : StrictlyPointed NF
SPNF = record
  { pointK[_]         = single
  ; pointK-bwd-member = Î» { here â†’ â‰¡-refl }
  }

PNF = StrictlyPointed.pointed SPNF

joinK : (k : K Î“) â†’ ForAllW k K â†’ K Î“
joinK (single _) h = h here
joinK nil        h = nil
joinK (cons x k) h = cons x (joinK k (h âˆ˜ there))

joinK-bwd-member : (k : K Î“) (h : ForAllW k K) â†’ ForAllW (joinK k h) (Î» Î” â†’ Existsâˆˆ k (Î» Î“âˆˆk â†’ Î” âˆˆ h Î“âˆˆk))
joinK-bwd-member (single _) h p
  = _ , here , p
joinK-bwd-member (cons x k) h (there p)
  = let (Î” , Î“âˆˆk , Î”âˆˆh-) = joinK-bwd-member k (h âˆ˜ there) p
    in _ , there Î“âˆˆk , Î”âˆˆh-

SJNF : StrictlyJoinable NF
SJNF = record
  { joinK            = joinK
  ; joinK-bwd-member = joinK-bwd-member
  }

JNF = StrictlyJoinable.joinable SJNF

-- imports USet, Cover' (the derived cover monad), etc.
open import USet.Base ğ• K _âˆˆ_ NF renaming (Cover' to Maybe')

Nf' : Ty â†’ USet
Nf' a = uset (Î» Î“ â†’ Nf Î“ a) wkNf

Ne' : Ty â†’ USet
Ne' a = uset (Î» Î“ â†’ Ne Î“ a) wkNe

emb' : Ne' ğ•“ â†’Ì‡ Nf' ğ•“
emb' .apply = emb

âŸ¦_âŸ§ : Ty â†’ USet
âŸ¦ ğ•“     âŸ§ = Nf' ğ•“
âŸ¦ a â‡’ b âŸ§ = âŸ¦ a âŸ§ â†’' âŸ¦ b âŸ§
âŸ¦ ğ• a   âŸ§ = Maybe' (âŸ¦ a âŸ§)

âŸ¦_âŸ§c : Ctx â†’ USet
âŸ¦ [] âŸ§c     = âŠ¤'
âŸ¦ Î“ `, a âŸ§c = âŸ¦ Î“ âŸ§c Ã—' âŸ¦ a âŸ§

--
-- Evaluation
--

nothing' : {G A : USet} â†’ G â†’Ì‡ Maybe' A
nothing' = fun (Î» _ â†’ nil , Î» {()})

just' : {G A : USet} â†’ G â†’Ì‡ A â†’ G â†’Ì‡ Maybe' A
just' = Return.return' PNF

letin' : {G A B : USet} â†’ (G â†’Ì‡ Maybe' A) â†’ ((G Ã—' A) â†’Ì‡ Maybe' B) â†’ (G â†’Ì‡ Maybe' B)
letin' {G} {A} {B} = Letin.letin' RNF JNF {G} {A} {B}

evalVar : Var Î“ a â†’  âŸ¦ Î“ âŸ§c â†’Ì‡ âŸ¦ a âŸ§
evalVar zero     = projâ‚‚'
evalVar (succ x) = evalVar x âˆ˜'  projâ‚'

eval : Tm Î“ a â†’ âŸ¦ Î“ âŸ§c â†’Ì‡ âŸ¦ a âŸ§
eval (var x)             = evalVar x
eval (lam t)             = lam' (eval t)
eval (app t u)           = app' (eval t) (eval u)
eval (nothing {a = a})   = nothing' {A = âŸ¦ a âŸ§}
eval (just t)            = just' (eval t)
eval (letin {b = b} t u) = letin' {B = âŸ¦ b âŸ§} (eval t) (eval u)

--
-- Residualisation
--

collect : Maybe' (Nf' a) â†’Ì‡ Nf' (ğ• a)
collect {a} = runCover {Nf' a} collectAux
  where
  collectAux : (k : K Î“) (f : ForAllW k (Nf' a â‚€_)) â†’ Nf' (ğ• a) â‚€ Î“
  collectAux (single _)  f = just (f here)
  collectAux nil         f = nothing
  collectAux (cons n k)  f = letin n (collectAux k (f âˆ˜ there))

register : Ne' (ğ• a) â†’Ì‡ Maybe' (Ne' a)
register {a} .apply {Î“} n = (cons n (single (Î“ `, a))) , Î» { (there here) â†’ var zero }

reify : âˆ€ a â†’ âŸ¦ a âŸ§ â†’Ì‡ Nf' a
reflect : âˆ€ a â†’ Ne' a â†’Ì‡ âŸ¦ a âŸ§

reify ğ•“       = id'
reify (a â‡’ b) = fun Î» f â†’ lam (reify b .apply (f freshWk (reflect a .apply (var zero))))
reify (ğ• a)   = collect âˆ˜' mapCover' (reify a)

reflect ğ•“       = emb'
reflect (a â‡’ b) = fun Î» n i x â†’ reflect b .apply (app (wkNe i n) (reify a .apply x))
reflect (ğ• a)   = mapCover' (reflect a) âˆ˜' register

--
-- NbE
--

idEnv : âˆ€ Î“ â†’ âŸ¦ Î“ âŸ§c â‚€ Î“
idEnv []       = _
idEnv (Î“ `, a) = wk âŸ¦ Î“ âŸ§c freshWk (idEnv Î“) , reflect a .apply (var zero)

quot : (âŸ¦ Î“ âŸ§c â†’Ì‡ âŸ¦ a âŸ§) â†’ Nf Î“ a
quot {Î“} {a} f = reify a .apply (f .apply (idEnv Î“))

norm : Tm Î“ a â†’ Nf Î“ a
norm = quot âˆ˜ eval
